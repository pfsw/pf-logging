apply plugin: 'java'

def includePath = project.pfswGradleIncludePath // usually specified in $GRADLE_USER_HOME/gradle.properties

version = '3.0.0'
archivesBaseName = "pf-logging"

ext {
  javaVersion = '1.6'
  localTestLibDir = "${buildDir}/test-libs"
  localTestArchive = "pf-logging-test-plugins.jar"
}

apply from: "${includePath}/common/ext-variables.gradle"
apply from: "${includePath}/common/repositories.gradle"
apply from: "${includePath}/java/java-compatibility.gradle"
apply from: "${includePath}/java/publishing-java-full.gradle"

dependencies {
  testCompile group: 'junit', name: 'junit', version: '4.12'
  testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
  // This is for Eclipse IDE.
  // The org.pfsw.logging.LoggerFactory cannot be in src/test/resources
  // because it would override the same file from src/main/resources
  // So it is in test-res and then packaged into the below JAR.
  testRuntime files("${localTestLibDir}/${localTestArchive}")
}

task createTestPluginsJar(type: Jar) {
  from file('test-res')
  archiveName localTestArchive
  destinationDir file(localTestLibDir)
}

test.dependsOn(createTestPluginsJar)

task wrapper(type: Wrapper) { gradleVersion = '2.14.1' }